/**
 * @fileOverview Khepri Statement parsers.
 */
package (
    blockStatement
    staticStatement
    variableStatement
    emptyStatement
    expressionStatement
    ifStatement
    withStatement
    iterationStatement
    continueStatement
    breakStatement
    returnStatement
    switchStatement
    throwStatement
    tryStatement
    debuggerStatement
    
    statement)
with
    import 'bennu::parse' {
        always
        attempt
        binds
        choice
        eager
        either
        enumeration
        expected
        many
        next
        optional
        Parser},
    import 'bennu::lang' {
        between
        sepBy1
        then},
    
    import 'khepri-ast::clause' ast_clause,
    import 'khepri-ast::declaration' ast_declaration,
    import 'khepri-ast::statement' ast_statement,
    
    import './common' {
        node
        nodea},
    import './token_parser' {keyword punctuator},
    import './expression_parser' {
        expression
        topLevelExpression},
    import './pattern_parser' {
        importPattern
        pattern},
    import './value_parser' {identifier}
in {

/* Forward declarations
 ******************************************************************************/
statement = \args(...) -> statement.apply(undefined, args);

/* Statement Parsers
 ******************************************************************************/
var logicalSemiColon = punctuator ';';

var statementList = eager(many(statement));

// Block Statement
////////////////////////////////////////
blockStatement = Parser@'Block Statement' <|
    node(
        between(punctuator '{', punctuator '}', 
            statementList),
        ast_statement.BlockStatement.create);

// Empty Statement
////////////////////////////////////////
emptyStatement = Parser@'Empty Statement' <|
    node(
        punctuator ';',
        ast_statement.EmptyStatement.create);

// Debugger Statement
////////////////////////////////////////
debuggerStatement = Parser@'Debugger Statement' <|
    node(
        next(
            keyword 'debugger',
            punctuator ';'),
        ast_statement.DebuggerStatement.create);

// Expression Statement
////////////////////////////////////////
expressionStatement = Parser('Expression Statement',
    node(
        then(
            topLevelExpression,
            logicalSemiColon),
        ast_statement.ExpressionStatement.create));

// Static Declaration Statement
////////////////////////////////////////
staticStatement = Parser('Static Statement', let
    staticDeclaration = node(
        identifier,
        ast_declaration.StaticDeclarator.create),
    
    staticDeclarationList =
        eager <| sepBy1(punctuator ',',
            staticDeclaration)
in
    node(
        between(keyword 'static', logicalSemiColon,
            staticDeclarationList),
        ast_declaration.StaticDeclaration.create));

// Variable Declaration Statement
////////////////////////////////////////
var variableDeclarationList = let
    initialiser = next(
        punctuator '=',
        expression |> expected@"variable initilizer"),

    variableDeclaration = nodea(
        enumeration(
            identifier,
            optional(null, initialiser)),
        ast_declaration.VariableDeclarator.create)
in
    eager <| sepBy1(punctuator ',',
        variableDeclaration);

variableStatement = Parser('Variable Statement',
    node(
        between(keyword 'var', logicalSemiColon,
            variableDeclarationList),
        ast_declaration.VariableDeclaration.create));

// With Statement
////////////////////////////////////////
withStatement = Parser@'With Statement' <| let
    withIdentifier = pattern |> expected@"pattern",

    withBinding = either(
        importPattern,
        nodea(
            enumeration(
                then(
                    withIdentifier,
                    punctuator '='),
                expression),
            ast_declaration.Binding.create)),
    
    bindings =
        eager <| sepBy1(punctuator ',',
            withBinding)
in
    nodea(
        next(
            keyword 'with',
            enumeration(
                bindings,
                next(
                    keyword 'in',
                    blockStatement))),
        ast_statement.WithStatement.create);

// If Statement
////////////////////////////////////////
ifStatement = Parser@'If Statement' <|
    nodea(
        next(
            keyword 'if',
            enumeration(
                between(punctuator '(', punctuator ')',
                    expression |> expected@"if condition"),
                statement,
                optional(null, next(
                    keyword 'else',
                    statement)))),
        ast_statement.IfStatement.create);

// Switch Statement
////////////////////////////////////////
switchStatement = Parser@'Switch Statement' <| let
    caseClause = nodea(
        next(
            keyword 'case',
            enumeration(
                then(
                    expression,
                    punctuator ':'),
                statementList)),
        ast_clause.SwitchCase.create),

    defaultClause = node(
        next(
            keyword 'default',
            next(
                punctuator ':',
                statementList)),
        \loc, consequent -> ast_clause.SwitchCase.create(loc, null, consequent)),

    caseClauses = eager <| many(caseClause),

    caseBlock = between(punctuator '{', punctuator '}',
        binds(
            enumeration(
                optional([], caseClauses),
                optional(null, defaultClause)),
            \first, defaultClause ->
                always(?defaultClause
                    :first.concat([defaultClause])
                    :first)))
in
    nodea(
        next(
            keyword 'switch',
            enumeration(
                between(punctuator '(', punctuator ')',
                    expression |> expected@"switch discriminant"),
                caseBlock)),
        ast_statement.SwitchStatement.create);

// Iteration Statement
////////////////////////////////////////
var whileStatement = Parser('While Statement',
    nodea(
        next(
            keyword 'while',
            enumeration(
                between(punctuator '(', punctuator ')',
                    expression),
                statement)),
        ast_statement.WhileStatement.create));

var doWhileStatement = Parser('Do While Statement',
    nodea(
        next(
            keyword 'do',
            enumeration(
                then(
                    statement,
                    keyword 'while'),
                between(punctuator '(', punctuator ')',
                    expression),
                punctuator ';')),
        ast_statement.DoWhileStatement.create));

var forStatement = Parser('For Statement', let
    forInitExpression = optional(null, 
        either(
            node(
                next(
                    keyword 'var',
                    variableDeclarationList),
                ast_declaration.VariableDeclaration.create),
            topLevelExpression)),
    
    forTestExpression = optional(null, expression),
    
    forUpdateExpression = optional(null, topLevelExpression)
in
    nodea(
        next(
            keyword 'for',
            enumeration(
                next(
                    punctuator '(',
                    forInitExpression),
                next(
                    punctuator ';',
                    forTestExpression),
                next(
                    punctuator ';',
                    forUpdateExpression),
                next(
                    punctuator ')',
                    statement))),
        ast_statement.ForStatement.create));

iterationStatement = Parser@'Iteration Statement' <|
    choice
        (doWhileStatement
        ,whileStatement
        ,forStatement);

// Continue Statement
////////////////////////////////////////
continueStatement = Parser@'Continue Statement' <|
    node(
        next(
            keyword 'continue',
            logicalSemiColon),
        ast_statement.ContinueStatement.create);

// Break Statement
////////////////////////////////////////
breakStatement = Parser@'Break Statement'  <|
    node(
        next(
            keyword 'break',
            logicalSemiColon),
        ast_statement.BreakStatement.create);

// Return Statement
////////////////////////////////////////
returnStatement = Parser@'Return Statement'  <|
    node(
        between(keyword 'return', logicalSemiColon,
            optional(null, expression)),
        ast_statement.ReturnStatement.create);

// Throw Statement
////////////////////////////////////////
throwStatement = Parser@'Throw Statement'  <|
    node(
        between(keyword 'throw', logicalSemiColon,
            expression),
        ast_statement.ThrowStatement.create);

// Try Statement
////////////////////////////////////////
tryStatement = Parser@'Try Statement'  <| let
    catchBlock = nodea(
        next(
            keyword 'catch',
            enumeration(
                between(punctuator '(', punctuator ')',
                    identifier),
                blockStatement)),
        ast_clause.CatchClause.create),

    finallyBlock = next(
        keyword 'finally',
        blockStatement)
in
    nodea(
        next(
            keyword 'try',
            enumeration(
                blockStatement,
                optional(null, catchBlock),
                optional(null, finallyBlock))),
        ast_statement.TryStatement.create);

// Statement
////////////////////////////////////////
statement = Parser@'Statement' <|
    choice
        (blockStatement
        ,staticStatement
        ,variableStatement
        ,emptyStatement
        ,ifStatement
        ,withStatement
        ,iterationStatement
        ,continueStatement
        ,breakStatement
        ,returnStatement
        ,switchStatement
        ,throwStatement
        ,tryStatement
        ,debuggerStatement
        ,expressionStatement)
    |> expected@'statement';

}